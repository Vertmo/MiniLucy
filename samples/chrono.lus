node chrono(StSt : bool; Rst : bool) returns (disp_1 : int; disp_2 : int);
var s : int; m : int; run : bool;
let
  automaton
  | CHRONO ->
    automaton
    | STOP ->
      s = 0 fby s;
      m = 0 fby m;
      run = false;
      until StSt then START;
    | START ->
      let d : int = 0 fby ((d + 1) mod 100) in
      s = if (d < (0 fby d)) 
          then (((0 fby s) + 1) mod 60)
          else (0 fby s);
      m = if (s < (0 fby s))
          then (((0 fby m) + 1) mod 60)
          else (0 fby m);
      run = true;
      until StSt then STOP;
    end;
  end;
  automaton
  | TIME ->
    disp_1 = s;
    disp_2 = m;
    until Rst and run then LAP;
  | LAP ->
    disp_1 = 0 fby disp_1;
    disp_2 = 0 fby disp_2;
    until Rst then TIME;
  end;
tel;